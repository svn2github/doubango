%{
/*
* Copyright (C) 2009 Mamadou Diop.
*
* Contact: Mamadou Diop <diopmamadou@yahoo.fr>
*	
* This file is part of Open Source Doubango Framework.
*
* DOUBANGO is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*	
* DOUBANGO is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU Lesser General Public License for more details.
*	
* You should have received a copy of the GNU General Public License
* along with DOUBANGO.
*
*/

/**@file tsip_transac_nist.sm
 * @brief SIP Non-INVITE Server Transaction as per RFC 3261 subclause 17.2.2.
 *
 * @author Mamadou Diop <diopmamadou(at)yahoo.fr>
 *
 * @date Created: Sat Nov 8 16:54:58 2009 mdiop
 */
%}

%class tsip_transac_nist
%header "tinysip/transactions/tsip_transac_nist.h"
%header "tinysip/tsip_message.h"
%start tsip_transac_nist::Started

%map tsip_transac_nist
%%


//------------------------
//------------------------
Started
	Entry
	{
		
	}
{
	sm_recv_request(request: const tsip_request_t*) 
		Trying 
		{
			Started_2_Trying_X_recv_request(request);
		}
}

//------------------------
//------------------------
Trying
	Entry
	{
		
	}
{
	sm_send_1xx(request: const tsip_request_t*)
		Proceeding
		{
			Trying_2_Proceeding_X_send_1xx(request);
		}

	sm_send_200_to_699(request: const tsip_request_t*) 
		Completed 
		{
			Trying_2_Completed_X_send_200_to_699(request);
		}

}

//------------------------
//------------------------
Proceeding
	Entry
	{
	}
{	
	sm_send_1xx(request: const tsip_request_t*)
		nil
		{
			Proceeding_2_Proceeding_X_send_1xx(request);
		}

	sm_send_200_to_699(request: const tsip_request_t*) 
		Completed 
		{
			Proceeding_2_Completed_X_send_200_to_699(request);
		}

	sm_recv_request(request: const tsip_request_t*) 
		nil 
		{
			Proceeding_2_Proceeding_X_recv_request(request);
		}
}


//------------------------
//------------------------
Completed
	Entry
	{
	}
{	
	sm_recv_request(request: const tsip_request_t*)
		nil
		{
			Completed_2_Completed_X_recv_request(request);
		}

	sm_timerJ()
		Terminated
		{
			Completed_2_Terminated_X_tirmerJ();
		}
}



//------------------------
//------------------------
Terminated
	Entry
	{
		OnTerminated();
	}
{
}

//------------------------
//------------------------
Default
{
	sm_transportError()
		Terminated
		{
			Any_2_Terminated_X_transportError();
		}

	Default
		nil 
		{
		}
}

%%