%{
/*
* Copyright (C) 2009 Mamadou Diop.
*
* Contact: Mamadou Diop <diopmamadou@yahoo.fr>
*	
* This file is part of Open Source Doubango Framework.
*
* DOUBANGO is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*	
* DOUBANGO is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU Lesser General Public License for more details.
*	
* You should have received a copy of the GNU General Public License
* along with DOUBANGO.
*
*/

/**@file tsip_dialog_register.sm.
 * @brief SIP dialog REGISTER.
 *
 * @author Mamadou Diop <diopmamadou(at)yahoo.fr>
 *
 * @date Created: Sat Nov 8 16:54:58 2009 mdiop
 */
%}


%class tsip_dialog_register
%header "tinysip/dialogs/tsip_dialog_register.h"
%start tsip_dialog_register::Started
%include "tsk_debug.h"

%map tsip_dialog_register
%%

//------------------------
//------------------------
Started
	Entry
	{
		
	}
{
	sm_send() 
		Trying 
		{
			Started_2_Trying_X_send();
		}
}

//------------------------
//------------------------
Trying
	Entry
	{
	}
{	
	sm_1xx()
		nil 
		{
			Trying_2_Trying_X_1xx();
		}

	sm_2xx(registering: int) [registering]
		Connected 
		{
			Trying_2_Connected_X_2xx();
		}

	sm_2xx(registering: int) [!registering]
		Terminated 
		{
			Trying_2_Terminated_X_2xx();
		}

	sm_401_407_421_494(succeed: int) [succeed]
		nil 
		{
			Trying_2_Trying_X_401_407_421_494();
		}

	sm_401_407_421_494(succeed: int) [!succeed]
		Terminated 
		{
			Trying_2_Terminated_X_401_407_421_494();
		}

	sm_423()
		nil 
		{
			Trying_2_Trying_X_423();
		}

	sm_300_to_699()
		Terminated 
		{
			Trying_2_Terminated_X_300_to_699();
		}

	sm_cancel()
		Terminated 
		{
			Trying_2_Terminated_X_cancel();
		}
}

//------------------------
//------------------------
Connected
	Entry
	{
	}
{	
	sm_unregister()
		Trying
		{
			Connected_2_Trying_X_unregister();
		}

	sm_refresh()
		Trying
		{
			Connected_2_Trying_X_refresh();
		}
}

//------------------------
//------------------------
Terminated
	Entry
	{
	}
{
}

//------------------------
//------------------------
Default 
	Entry 
	{ 
	}
{
	sm_transportError()
		Terminated
		{
			Any_2_Terminated_X_transportError();
		}
	Default
		nil 
		{
		}
}
%%