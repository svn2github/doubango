%{
/*
* Copyright (C) 2009 Mamadou Diop.
*
* Contact: Mamadou Diop <diopmamadou@yahoo.fr>
*	
* This file is part of Open Source Doubango Framework.
*
* DOUBANGO is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*	
* DOUBANGO is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU Lesser General Public License for more details.
*	
* You should have received a copy of the GNU General Public License
* along with DOUBANGO.
*
*/

/**@file tsip_dialog_message.sm.
 * @brief SIP dialog MESSAGE.
 *
 * @author Mamadou Diop <diopmamadou(at)yahoo.fr>
 *
 * @date Created: Sat Nov 8 16:54:58 2009 mdiop
 */
%}


%class tsip_dialog_message
%header "tinysip/dialogs/tsip_dialog_message.h"
%header "tinysip/tsip_message.h"
%start tsip_dialog_message::Started
%include "tsk_debug.h"

%map tsip_dialog_message
%%

//------------------------
//------------------------
Started
{
	sm_sendMESSAGE(msg: const tsip_message_t*) 
		Sending 
		{
			Started_2_Sending_X_sendMESSAGE(msg);
		}
	sm_receiveMESSAGE(msg: const tsip_message_t*)
		Receiving
		{
			Started_2_Receiving_X_recvMESSAGE(msg);
		}
}

//------------------------
//------------------------
Sending
{	
	sm_1xx(msg: const tsip_message_t*)
		nil 
		{
			Sending_2_Sending_X_1xx(msg);
		}

	sm_2xx(msg: const tsip_message_t*)
		Terminated 
		{
			Sending_2_Terminated_X_2xx(msg);
		}

	sm_401_407_421_494(msg: const tsip_message_t*)
		nil 
		{
			Sending_2_Sending_X_401_407_421_494(msg);
		}

	sm_300_to_699(msg: const tsip_message_t*)
		Terminated 
		{
			Sending_2_Terminated_X_300_to_699(msg);
		}

	sm_cancel()
		Terminated 
		{
			Sending_2_Terminated_X_cancel();
		}
}

//------------------------
//------------------------
Receiving
{	
	sm_accept(msg: const tsip_message_t*)
		Terminated
		{
			Receiving_2_Terminated_X_accept(msg);
		}

	sm_reject(msg: const tsip_message_t*)
		Terminated
		{
			Receiving_2_Terminated_X_reject(msg);
		}
}

//------------------------
//------------------------
Terminated
	Entry
	{
		OnTerminated();
	}
{
	Default
		nil 
		{
		}
}

//------------------------
//------------------------
Default 
{
	sm_transportError()
		Terminated
		{
			Any_2_Terminated_X_transportError();
		}

	sm_error()
		Terminated
		{
		}

	Default
		nil 
		{
		}
}
%%