%{
//	** Copyright (C) 2009 Doubango Telecom <imsframework(at)gmail.com> 
//
//	This file is part of 'Doubango IMS Client Framework'.
//
//    'Doubango IMS Client Framework' is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    'Doubango IMS Client Framework' is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//   GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//  along with 'Doubango IMS Client Framework'.  If not, see <http://www.gnu.org/licenses/>.
//
//	** For commercial use, you must purchase a proprietary license from Doubango Telecom.
//
//
///* Copyright (C) 2009 Doubango Telecom
//*
//* Authors:	F0076RTY <imsframework@gmail.com>
//*
//*/
//
%}

%class sip_dialog_register
%header sip_dialog_register.h
%package dgo // PREF_NAMESPACE_VALUE ==> see 'pref.h' for more information
%start map_dialog_register::Initialized

%map map_dialog_register
%%

//-------------------------------------------------------------------------------------
// Initialized
//-------------------------------------------------------------------------------------
Initialized Entry { OnStateChanged(SS_REGISTER_INITIALIZED); }
{
	sm_registerSent()	Trying { }
}
	
Trying Entry { OnStateChanged(SS_REGISTER_TRYING); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unreg: bool)	[unreg == true]		Terminated {}
	sm_2xx_response(unreg: bool)	[unreg == false]	Established {}
	sm_3xx_response()				Terminated {}
	sm_401_407_421_494_response()	Authentifying {}
	sm_4xx_response()				Terminated {}
	sm_5xx_response()				Terminated {}
	sm_6xx_response()				Terminated {}
	sm_xxx_response()				Terminated {}
	sm_cancelSent()					Terminated {}
	Default							nil {}
}

Established Entry { OnStateChanged(SS_REGISTER_ESTABLISHED); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unreg: bool)	[unreg == true]		Terminated {}
	sm_2xx_response(unreg: bool)	[unreg == false]	nil {}
	sm_401_407_421_494_response()	Authentifying {}
	sm_unregisterSent()				Trying { }
	sm_xxx_response()				Terminated {}
	Default							nil {}
}

Authentifying  Entry { OnStateChanged(SS_REGISTER_AUTHENTIFYING); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unreg: bool)	[unreg == true]		Terminated {}
	sm_2xx_response(unreg: bool)	[unreg == false]	Established {}
	sm_xxx_response()				Terminated {}
	sm_authentificationSent()		Trying {}
	Default							nil {}
}

Terminated  Entry { OnStateChanged(SS_REGISTER_TERMINATED); }
{
	Default nil {}
}

Default Entry { OnStateChanged(SS_REGISTER_UNKNOWN); }
{
	sm_401_407_421_494_response()	Authentifying {}
	sm_3xx_response()				nil {}
	sm_4xx_response()				nil {}
	sm_5xx_response()				nil {}
	sm_6xx_response()				nil {}
	sm_xxx_response()				nil {}
	Default							nil {}
}

%%