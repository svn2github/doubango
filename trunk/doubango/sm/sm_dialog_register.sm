%{
///****************************************************************************
//			 _             _                             
//			| |           | |                            
//		  _ | | ___  _   _| | _   ____ ____   ____  ___  
//		 / || |/ _ \| | | | || \ / _  |  _ \ / _  |/ _ \ 
//		( (_| | |_| | |_| | |_) | ( | | | | ( ( | | |_| |
//		 \____|\___/ \____|____/ \_||_|_| |_|\_|| |\___/ 
//											(_____|   
//	
//	Copyright (C) 2009 xxxyyyzzz <imsframework(at)gmail.com>
//
//	This file is part of Open Source Doubango IMS Client Framework project.
//
//    DOUBANGO is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//	
//    DOUBANGO is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU Lesser General Public License for more details.
//	
//    You should have received a copy of the GNU General Public License
//    along with DOUBANGO.
//****************************************************************************/
%}

%class sip_dialog_register
%header sip_dialog_register.h
%package dgo // PREF_NAMESPACE_VALUE ==> see 'pref.h' for more information
%start map_dialog_register::Initialized

%map map_dialog_register
%%

//-------------------------------------------------------------------------------------
// Initialized
//-------------------------------------------------------------------------------------
Initialized Entry { OnStateChanged(srs_none); }
{
	sm_registerSent()	Trying { }
}
	
Trying Entry { OnStateChanged(srs_trying); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unreg: bool)	[unreg == true]		Terminated {}
	sm_2xx_response(unreg: bool)	[unreg == false]	Established {}
	sm_3xx_response()				Terminated {}
	sm_401_407_421_494_response()	Authentifying {}
	sm_4xx_response()				Terminated {}
	sm_5xx_response()				Terminated {}
	sm_6xx_response()				Terminated {}
	sm_xxx_response()				Terminated {}
	sm_cancelSent()					Terminated {}
	Default							nil {}
}

Established Entry { OnStateChanged(srs_registered); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unreg: bool)	[unreg == true]		Terminated {}
	sm_2xx_response(unreg: bool)	[unreg == false]	nil {}
	sm_401_407_421_494_response()	Authentifying {}
	sm_unregisterSent()				Trying { }
	sm_xxx_response()				Terminated {}
	Default							nil {}
}

Authentifying  Entry { OnStateChanged(srs_authentifying); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unreg: bool)	[unreg == true]		Terminated {}
	sm_2xx_response(unreg: bool)	[unreg == false]	Established {}
	sm_xxx_response()				Terminated {}
	sm_authentificationSent()		Trying {}
	Default							nil {}
}

Terminated  Entry { OnStateChanged(srs_unregistered); }
{
	Default nil {}
}

Default Entry { OnStateChanged(srs_none); }
{
	sm_401_407_421_494_response()	Authentifying {}
	sm_3xx_response()				nil {}
	sm_4xx_response()				nil {}
	sm_5xx_response()				nil {}
	sm_6xx_response()				nil {}
	sm_xxx_response()				nil {}
	Default							nil {}
}

%%