%{
///**
//* @file
//* @author  xxxyyyzzz <imsframework(at)gmail.com>
//* @version 1.0
//*
//* @section LICENSE
//*
//*	
//* This file is part of Open Source Doubango IMS Client Framework project.
//*
//* DOUBANGO is free software: you can redistribute it and/or modify
//* it under the terms of the GNU General Public License as published by
//* the Free Software Foundation, either version 3 of the License, or
//* (at your option) any later version.
//*	
//* DOUBANGO is distributed in the hope that it will be useful,
//* but WITHOUT ANY WARRANTY; without even the implied warranty of
//* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//* GNU Lesser General Public License for more details.
//*	
//* You should have received a copy of the GNU General Public License
//* along with DOUBANGO.
//*
//* @section DESCRIPTION
//*
//*
//*/
%}

%class sip_dialog_subscribe
%header sip_dialog_subscribe.h
%package dgo // PREF_NAMESPACE_VALUE ==> see 'pref.h' for more information
%start map_dialog_subscribe::Initialized

%map map_dialog_subscribe
%%

//-------------------------------------------------------------------------------------
// Initialized
//-------------------------------------------------------------------------------------
Initialized Entry { OnStateChanged(SS_SUBSCRIBE_INITIALIZED); }
{
	sm_subscribeSent()	Trying { }
}
	
Trying Entry { OnStateChanged(SS_SUBSCRIBE_TRYING); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unsub: bool)	[unsub == true]		Terminated {}
	sm_2xx_response(unsub: bool)	[unsub == false]	Established {}
	sm_3xx_response()				Terminated {}
	sm_401_407_421_494_response()	Authentifying {}
	sm_4xx_response()				Terminated {}
	sm_5xx_response()				Terminated {}
	sm_6xx_response()				Terminated {}
	sm_xxx_response()				Terminated {}
	sm_notify_response(term: bool)	[term == true]		Terminated { OnStateChanged(SS_SUBSCRIBE_NOTIFIED); }
	sm_notify_response(term: bool)	[term == false]		nil { OnStateChanged(SS_SUBSCRIBE_NOTIFIED); }
	sm_cancelSent()					Terminated {}
	Default							nil {}
}

Established Entry { OnStateChanged(SS_SUBSCRIBE_ESTABLISHED); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unsub: bool)	[unsub == true]		Terminated {}
	sm_2xx_response(unsub: bool)	[unsub == false]	nil {}
	sm_401_407_421_494_response()	Authentifying {}
	sm_unsubscribeSent()			Trying { }
	sm_xxx_response()				Terminated {}
	sm_notify_response(term: bool)	[term == true]		Terminated { OnStateChanged(SS_SUBSCRIBE_NOTIFIED);}
	sm_notify_response(term: bool)	[term == false]	nil { OnStateChanged(SS_SUBSCRIBE_NOTIFIED); }
	Default							nil {}
}

Authentifying  Entry { OnStateChanged(SS_SUBSCRIBE_AUTHENTIFYING); }
{
	sm_1xx_response()				nil {}
	sm_2xx_response(unsub: bool)	[unsub == true]		Terminated {}
	sm_2xx_response(unsub: bool)	[unsub == false]	Established {}
	sm_xxx_response()				Terminated {}
	sm_authentificationSent()		Trying {}
	Default							nil {}
}

Terminated  Entry { OnStateChanged(SS_SUBSCRIBE_TERMINATED); }
{
	Default nil {}
}

Default Entry { OnStateChanged(SS_SUBSCRIBE_UNKNOWN); }
{
	sm_401_407_421_494_response()	Authentifying {}
	sm_3xx_response()				nil {}
	sm_4xx_response()				nil {}
	sm_5xx_response()				nil {}
	sm_6xx_response()				nil {}
	sm_xxx_response()				nil {}
	Default							nil {}
}

%%